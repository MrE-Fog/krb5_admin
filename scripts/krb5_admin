#!/usr/pkg/bin/perl

use Getopt::Std;

use Kharon::Entitlement::SimpleSQL;

use Krb5Admin::KerberosDB;
use Krb5Admin::Client;
use Krb5Admin::CLI;

use strict;
use warnings;

#
# Global variables:

my $debug = 0;
my $verbose = 0;
my $terse = 0;

sub usage {
	print STDERR <<EOM;
usage: krb5_admin [-v] [-D dbname] [-h kdc_host | -r realm | -l] user [user ...]
EOM
	exit(1);
}

my %opts;
my $dbname;
my $sqldbname;
my @kdcs;
my $realm;

getopts('D:S:dh:lr:sv?', \%opts) or usage();

usage()			if exists($opts{'?'});
$dbname = $opts{'D'}	if exists($opts{'D'});
$sqldbname = $opts{'S'}	if exists($opts{'S'});
$debug = 1		if exists($opts{'d'});
$verbose = 1		if exists($opts{'v'});
$terse = 1		if exists($opts{'s'});
@kdcs = ($opts{'h'})	if exists($opts{'h'});
$realm = $opts{'r'}	if exists($opts{'r'});

$opts{'l'} = 1		if defined($dbname);

if (@kdcs + ($opts{l}?1:0) + ($opts{r}?1:0) > 1) {
	print STDERR "-h, -l, and -r are mutually exclusive.\n";
	usage();
}

if (defined($sqldbname) && !$opts{'l'}) {
	print STDERR "-S may only be used in conjunction with -l or -D.\n";
	usage();
}

my $cli = Krb5Admin::CLI->new(appname => 'krb5_admin');

my $kmdb;
eval {
	if (exists($opts{'l'})) {
		my $sqlacl  = Kharon::Entitlement::SimpleSQL->new(
		    table => 'krb5_admin_simple_acls');
		$kmdb = Krb5Admin::KerberosDB->new(local=>1, dbname=>$dbname,
		    sqlite=>$sqldbname, sacls=>$sqlacl);
		$sqlacl->set_dbh($kmdb->get_dbh());
	} else {
		my %args;
		$args{realm} = $realm	if defined($realm);
		$kmdb = Krb5Admin::Client->new(undef, \%args, @kdcs);
	}
};
if (my $err = $@) {
	$cli->printerr($err);
	exit(1);
}

$cli->set_obj($kmdb);

exit($cli->run_cmd(@ARGV))	if $#ARGV >= 0;
exit($cli->run_cmdline());
